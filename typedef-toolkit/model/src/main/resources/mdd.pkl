/* SPDX-License-Identifier: Apache-2.0
   Copyright 2024 Atlan Pte. Ltd. */
amends "Model.pkl"

t = "Cube"
a = t.toLowerCase()

local dimension = "Dimension"
local hierarchy = "Hierarchy"
local field = "Field"
local qualifiedName = "QualifiedName"
local supertypeName = "MDD"
local attrName = "Name"

shared {
  supertypeDefinition {
    name = supertypeName
    description = "A dataset with multiple dimensions"
    superTypes { "Catalog" }
    attributes {
      ["\(a)\(attrName)"] {
        label = "Cube"
        description = "Simple name of the cube in which this asset exists, or empty if it is itself a cube."
        type = "string"
        indexAs = "both"
      }
      ["\(a)\(qualifiedName)"] {
        description = "Unique name of the cube in which this asset exists, or empty if it is itself a cube."
        type = "string"
        indexAs = "keyword"
      }
      ["\(a)\(dimension)\(attrName)"] {
        label = "Dimension"
        description = "Simple name of the cube dimension in which this asset exists, or empty if it is itself a dimension."
        type = "string"
        indexAs = "both"
      }
      ["\(a)\(dimension)\(qualifiedName)"] {
        description = "Unique name of the cube dimension in which this asset exists, or empty if it is itself a dimension."
        type = "string"
        indexAs = "keyword"
      }
      ["\(a)\(hierarchy)\(attrName)"] {
        label = "Hierarchy"
        description = "Simple name of the dimension hierarchy in which this asset exists, or empty if it is itself a hierarchy."
        type = "string"
        indexAs = "both"
      }
      ["\(a)\(hierarchy)\(qualifiedName)"] {
        description = "Unique name of the dimension hierarchy in which this asset exists, or empty if it is itself a hierarchy."
        type = "string"
        indexAs = "keyword"
      }
    }
  }
  ui {
    svgName = "MultiDimensional.svg"
    filters {
      [t] {
        label = t
        attribute = "\(a)\(qualifiedName)"
        image = "CubeGray"
      }
      ["\(t)\(dimension)"] {
        label = dimension
        attribute = "\(a)\(dimension)\(qualifiedName)"
        image = "CubeDimensionGray"
      }
    }
  }
}

customTypes {
  [t] {
    label = t
    description = "Instance of a cube in Atlan."
    attributes {
      ["\(a)\(dimension)Count"] {
        labelPlural = "Dimensions"
        description = "Number of dimensions in the cube."
        type = "long"
      }
    }
  }
  ["\(t)\(dimension)"] {
    label = dimension
    description = "Instance of a cube dimension in Atlan."
    attributes {
      ["\(a)\(hierarchy)Count"] {
        labelPlural = "Hierarchies"
        description = "Number of hierarchies in the cube dimension."
        type = "long"
      }
    }
    relationships {
      [a] {
        description = "Containment relationship between \(t) (parent) and \(t)\(dimension) (children)."
        endDef1 {
          description = "Individual dimensions that make up the cube."
          type = t
          name = "\(a)\(dimension)s"
          isContainer = true
        }
        endDef2 {
          description = "Cube in which this dimension exists."
          type = "\(t)\(dimension)"
          name = a
        }
      }
    }
  }
  ["\(t)\(hierarchy)"] {
    label = hierarchy
    description = "Instance of a cube hierarchy in Atlan."
    attributes {
      ["\(a)\(field)Count"] {
        labelPlural = "Fields"
        description = "Number of total fields in the cube hierarchy."
        type = "long"
      }
    }
    relationships {
      ["\(a)\(dimension)"] {
        description = "Containment relationship between \(t)\(dimension) (parent) and \(t)\(hierarchy) (children)."
        endDef1 {
          description = "Individual hierarchies that make up the dimension."
          type = "\(t)\(dimension)"
          name = "\(a)Hierarchies"
          isContainer = true
        }
        endDef2 {
          description = "Dimension in which this hierarchy exists."
          type = "\(t)\(hierarchy)"
          name = "\(a)\(dimension)"
        }
      }
    }
  }
  ["\(t)\(field)"] {
    label = field
    description = "Instance of a cube field in Atlan."
    attributes {
      ["\(a)Parent\(field)Name"] {
        label = "Parent field"
        description = "Name of the parent field in which this field is nested."
        type = "string"
        indexAs = "both"
      }
      ["\(a)Parent\(field)QualifiedName"] {
        description = "Unique name of the parent field in which this field is nested."
        type = "string"
        indexAs = "keyword"
      }
      ["\(a)\(field)Level"] {
        label = "Level"
        description = "Level of the field in the cube hierarchy."
        type = "long"
      }
      ["\(a)\(field)MeasureExpression"] {
        label = "Measure expression"
        description = "Expression used to calculate this measure."
        type = "string"
        indexAs = "both"
      }
      ["\(a)Sub\(field)Count"] {
        labelPlural = "Subfields"
        description = "Number of sub-fields that are direct children of this field."
        type = "long"
      }
    }
    relationships {
      ["\(a)\(hierarchy)"] {
        description = "Containment relationship between \(t)\(hierarchy) (parent) and \(t)\(field) (children)."
        endDef1 {
          description = "Individual fields that make up the hierarchy."
          type = "\(t)\(hierarchy)"
          name = "\(a)\(field)s"
          isContainer = true
        }
        endDef2 {
          description = "Hierarchy in which this field exists."
          type = "\(t)\(field)"
          name = "\(a)\(hierarchy)"
        }
      }
      ["\(a)Parent\(field)"] {
        description = "Hierarchical nesting relationship between \(t)\(field)s."
        endDef1 {
          description = "Individual fields nested within this cube field."
          type = "\(t)\(field)"
          name = "\(a)Nested\(field)s"
          isContainer = true
        }
        endDef2 {
          description = "Parent field in which this field is nested."
          type = "\(t)\(field)"
          name = "\(a)Parent\(field)"
        }
      }
    }
  }
}
