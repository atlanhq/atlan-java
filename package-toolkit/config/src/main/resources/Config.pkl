/* SPDX-License-Identifier: Apache-2.0
   Copyright 2024 Atlan Pte. Ltd. */

/// Template for defining configuration for a custom package in Atlan.
///
/// | Variable | | Usage | Default |
/// |---|---|---|---|
/// | **packageId** | | Unique identifier for the package, including its namespace. | |
/// | **packageName** | | Display name for the package, as it should be shown in the UI. | |
/// | **version** | | Version of this package, following semantic versioning. | |
/// | **description** | | Description for the package, as it should be shown in the UI. | |
/// | **iconUrl** | | Link to an icon to use for the package, as it should be shown in the UI. | |
/// | **docsUrl** | | Link to an online document describing the package. | |
/// | **uiConfig** | | Configuration for the UI of the custom package. | |
/// | **implementationLanguage** | | Coding language the package is implemented in. | |
/// | **containerImage** | | Container image to run the logic of the custom package. | |
/// | **containerCommand** | | Full command to run in the container image, as a list rather than spaced. | |
/// | containerImagePullPolicy | | (Optional) Override the default IfNotPresent policy. | `IfNotPresent` |
/// | outputs | | (Optional) Any outputs that the custom package logic is expected to produce. | |
/// | keywords | | (Optional) Any keyword labels to apply to the package. | |
/// | allowSchedule | | (Optional) Whether to allow the package to be scheduled (true) or only run immediately (false). | `true` |
/// | certified | | (Optional) Whether the package should be listed as certified (true) or not (false). | `true` |
/// | preview | | (Optional) Whether the package should be labeled as an early preview in the UI (true) or not (false). | `false` |
/// | connectorType | | (Optional) If the package needs to configure a connector, specify its type here. | |
/// | category | | Name of the pill under which the package should be categorized in the marketplace in the UI. | `custom` |
@ModuleInfo { minPklVersion = "0.25.1" }
open module com.atlan.pkg.Config

import "pkl:semver"
import "Renderers.pkl"
import "Connectors.pkl"

/// Unique identifier for the package, including its namespace.
/// For example: @csa/open-api-spec-loader
packageId: String(matches(Regex("@[a-z0-9-]+/[a-z0-9-]+")))

/// Display name for the package, as it should be shown in the UI.
packageName: String

/// Version of this package, following semantic versioning.
version: semver.Version

/// Description for the package, as it should be shown in the UI.
description: String

/// Link to an icon to use for the package, as it should be shown in the UI.
iconUrl: String

/// Link to an online document describing the package.
docsUrl: String

/// Configuration for the UI of the custom package.
///
/// | Variable | | Usage |
/// |---|---|---|
/// | **[tasks][UIStep]** | | Mapping of top-level tasks to be configured, keyed by the name of the task as it should appear in the UI. |
/// | **[rules][UIRule]** | | Listing of rules to control which inputs appear based on values selected in other inputs. |
uiConfig: UIConfig

/// Coding language the package is implemented in.
/// This will control what (if any) strongly-typed configuration hand-over classes are generated by the toolkit.
/// (Note: if using 'Other', no strongly-typed configuration hand-over classes will be generated.)
implementationLanguage: CodeLanguage

/// Container image to run the logic of the custom package.
containerImage: String

/// Full command to run in the container image, as a list rather than spaced.
containerCommand: Listing<String>

/// (Optional) Override the default IfNotPresent policy.
containerImagePullPolicy: ImagePullPolicy = "IfNotPresent"

/// (Optional) Any outputs that the custom package logic is expected to produce.
outputs: WorkflowOutputs?

/// (Optional) Any keyword labels to apply to the package.
keywords: Listing<String> = new Listing {}

/// (Optional) Whether to allow the package to be scheduled (default, true) or only run immediately (false).
allowSchedule: Boolean = true

/// (Optional) Whether the package should be listed as certified (default, true) or not (false).
certified: Boolean = true

/// (Optional) Whether the package should be labeled as an early preview in the UI (true) or not (default, false).
preview: Boolean = false

/// (Optional) If the package needs to configure a connector, specify its type here.
connectorType: Connectors.Type?

/// Name of the pill under which the package should be categorized in the marketplace in the UI.
category: String = "custom"

// --- CONTENT ABOVE THIS LINE IS INTENDED INPUT ---
// --- CONTENT BELOW THIS LINE IS USED TO GENERATE OUTPUTS ---

/// (Generated) Command to run in the container image.
fixed command: List<String> = List(containerCommand[0])

/// (Generated) Arguments to provide to the command that runs in the container image.
fixed args: List<String> = containerCommand.toList().sublist(1, containerCommand.length)

/// Configuration for the UI of the custom package.
///
/// | Variable | | Usage |
/// |---|---|---|
/// | **[tasks][UIStep]** | | Mapping of top-level tasks to be configured, keyed by the name of the task as it should appear in the UI. |
/// | **[rules][UIRule]** | | Listing of rules to control which [inputs][UIElement] appear based on values selected in other inputs. |
class UIConfig {

  /// Mapping of top-level tasks to be configured, keyed by the name of the task as it should appear in the UI.
  ///
  /// Remember in Pkl to define a mapping, put the key in square brackets and the value in curly braces:
  /// ```
  /// tasks {
  ///   ["Input"] {
  ///     ...
  ///   }
  ///   ["Delivery"] {
  ///     ...
  ///   }
  ///   ...
  /// }
  /// ```
  hidden tasks: Mapping<String, UIStep>

  /// (Generated) Details of all inputs across all steps for the UI.
  fixed properties: Map<String, UIElement> = new Mapping {
    for (_, step in tasks) {
      // Note: using spread syntax to error on any duplicates across steps
      ...?step.inputs
    }
  }.toMap()

  /// Listing of rules to control which [inputs][UIElement] appear based on values selected in other inputs.
  ///
  /// Remember in Pkl to define a listing, use curly braces and create new elements within:
  /// ```
  /// rules {
  ///   new UIRule {
  ///     whenInputs { ["export_scope"] = "ENRICHED_ONLY" }
  ///     required = { "qn_prefix" }
  ///   }
  ///   new UIRule {
  ///     ...
  ///   }
  ///   ...
  /// }
  /// ```
  hidden rules: Listing<UIRule> = new Listing {}

  /// (Generated) Details of all UI rules to use to control the UI.
  fixed anyOf: List<UIRule>? = if (rules.isEmpty) null else rules.toList()

  /// (Generated) Details about each task (top-level step) to use in the UI.
  fixed steps: List<UIStep> = tasks.fold(List(), (acc: List<UIStep>, key, value) ->
    acc.add(resolveStep(key, value))
  )
}

const function resolveInputs(inputs: Mapping<String, UIElement>): Mapping<String, UIElement> = inputs.fold(new Mapping {}, (acc: Mapping<String, UIElement>, inputKey, input) ->
  (acc) {
    [inputKey] = input
  }
)

const function resolveStep(stepName: String, step: UIStep): UIStep = (step) {
  title = stepName
}

/// Configure a top-level step in the UI for the package.
///
/// | Field | | Description |
/// |---|---|---|
/// | **`title`** | | name to show in the UI for the step |
/// | `description` | | description to show in the UI for the step |
/// | **[inputs][UIElement]** | | mapping of inputs to be configured for the step, keyed by a unique (variable) name for the input |
class UIStep {

  /// Name to show in the UI for the step.
  /// This will be set automatically from the key of the outer map in which this step is defined.
  title: String

  /// Description to show in the UI for the step.
  description: String = ""

  /// Mapping of [inputs][UIElement] to be configured for the step, keyed by a unique (variable) name for the input.
  /// Note: the name of the variable (key of the map) should be in lower_snake_case.
  ///
  /// Remember in Pkl to define a mapping, put the key in square brackets and the value in curly braces:
  /// ```
  /// inputs {
  ///   ["export_scope"] = new Radio {
  ///     ...
  ///   }
  ///   ["qn_prefix"] = new TextInput {
  ///     ...
  ///   }
  ///   ...
  /// }
  /// ```
  hidden inputs: Mapping<String(lower_snake_case), UIElement>

  /// (Generated) Unique identifier for the step.
  fixed id = title.replaceAll(" ", "_").toLowerCase()

  /// (Generated) List of inputs contained within the step.
  fixed properties: List<String> = inputs.fold(List(), (acc, key, _) -> acc.add(key))
}

/// Validates the provided string value is in snake_case.
local const lower_snake_case = (str) -> str.matches(Regex("[a-z0-9_]*"))

/// Configure basic UI rules that when the specified inputs have specified values, certain
/// other fields become required.
///
/// | Field | | Description |
/// |---|---|---|
/// | **`whenInputs`** | | mapping from input ID to value for the step |
/// | **`required`** | | list of input IDs that should become required when the inputs all match |
class UIRule {
  fixed properties: Mapping<String, Mapping<String, String>> = new Mapping {
    for (k, v in whenInputs) {
      [k] = new Mapping {
        ["const"] = v
      }
    }
  }

  /// Mapping from input ID (variable name for the input) to value for the step
  hidden whenInputs: Mapping<String, String>

  /// List of input IDs (variable names) that should become required when the inputs all match
  required: Listing<String>
}

/// Class defining any outputs the package's logic will produce.
class WorkflowOutputs {
  /// Files the package will produce in the local filesystem.
  hidden files: Mapping<String, String>?

  /// Outputs the package will produce into S3.
  s3Objects: Listing<S3Artifact>?

  /// (Generated) List of all artifacts the package will produce.
  fixed artifacts: List<Renderers.NamedPair> = (s3Objects?.toList() ?? List())
    + (files?.fold(List(), (acc: List<Renderers.NamedPair>, key, value) ->
    acc.add(new Renderers.NamePathPair {
      name=key
      path=value
    })) ?? List())
}

class S3Artifact extends Renderers.NamedPair {
  /// Name of the object in S3
  name: String

  /// Path on the container's local filesystem for the object
  path: String = "/tmp/\(name).json"

  /// (Optional) Configuration for how the S3 object should be archived.
  archive: Mapping<String, Mapping<String, String>> = new Mapping {
    ["none"] = new Mapping {}
  }

  /// (Optional) Configuration for how the S3 object should be mapped.
  s3: Mapping<String, String> = new Mapping {
    ["key"] = "{{inputs.parameters.\(Renderers.S3_CONFIG_PREFIX)}}/\(name).json"
  }

  /// (Optional) Configuration for how the S3 object should be garbage collected.
  artifactGC: Mapping<String, String> = new Mapping {
    ["strategy"] = "OnWorkflowDeletion"
  }
}

/// Image pull policy for the container image.
/// For more details on behavior, see: https://kubernetes.io/docs/concepts/containers/images/#image-pull-policy
typealias ImagePullPolicy = "Always"|"IfNotPresent"|"Never"

/// Coding languages the toolkit can generate strongly-typed configuration hand-over classes for.
typealias CodeLanguage = "Java"|"Kotlin"|"Python"|"Other"

/// (Generated) Unique name to use for file-based objects for the package.
hidden fixed name = packageId.replaceAll("@", "").replaceAll("/", "-")

const function getPythonPkgName(m): String = getLowerSnakeCase(getPascalCase(m.packageName))

/// Set up multiple outputs for the module, one for each configuration file.
/// - `m` the package config to generate outputs for
const function getOutputs(m): Mapping<String, FileOutput> = new Mapping {
  ["build/package/\(m.name)/configmaps/default.yaml"] = Renderers.getConfigMap(m)
  ["build/package/\(m.name)/templates/default.yaml"] = Renderers.getWorkflowTemplate(m)
  ["build/package/\(m.name)/index.js"] = Renderers.getIndexJs()
  ["build/package/\(m.name)/package.json"] = Renderers.getPackageJson(m)
  when (m.implementationLanguage == "Kotlin" || m.implementationLanguage == "Java") {
    ["src/main/kotlin/\(getClassName(m.packageName)).kt"] = Renderers.getConfigClassKt(m, getClassName(m.packageName))
  }
  when (m.implementationLanguage == "Python") {
    ["\(getPythonPkgName(m))/\(getPythonPkgName(m))_cfg.py"] = Renderers.getConfigClassPy(m)
    ["\(getPythonPkgName(m))/__init__.py"] = Renderers.getBlankFile()
    ["\(getPythonPkgName(m))/main.py.example"] = Renderers.getMainPy(getPythonPkgName(m))
    ["\(getPythonPkgName(m))/logging.conf"] = Renderers.getLoggingConfPy()
    ["requirements.txt.example"] = Renderers.getRequirementsPy()
    ["requirements-dev.txt.example"] = Renderers.getRequirementsDevPy()
    ["version.txt"] = Renderers.getVersionPy(m)
    ["Dockerfile"] = Renderers.getDockerfilePy(getPythonPkgName(m))
  }
}

/// Translate the model content into a set of files for both type definitions (JSON)
/// and UI configuration (TypeScript).
const function getModuleOutput(m): ModuleOutput = new ModuleOutput {
  files = getOutputs(m)
}

/// Set the output of the module to be separate files for each custom type definition in the model.
output = getModuleOutput(this)

/// Turn the provided name into a PascalCase name.
const function getPascalCase(name: String): String =
  name.split(Regex("[\\W_]+")).map((word) -> word.capitalize()).join("")

/// Turn the provided name into a lower_snake_case_name.
const function getLowerSnakeCase(text: String): String = getSnakeCase(text).toLowerCase()
local const function getSnakeCase(text: String): String =
  text.replaceAll("_", "").replaceAll(Regex("([A-Z0-9]+)([A-Z][a-z][0-9])"), "$1_$2").replaceAll(Regex("([a-z0-9])([A-Z0-9])"), "$1_$2")

const function getClassName(name: String): String = "\(getPascalCase(name))Cfg"

// --- WIDGETS ---
/// Base class for all UI elements that can be used in the configuration of a package.
///
/// You will not use this class directly, but will use one of its subclasses:
///
/// | Widget | | Usage |
/// |---|---|---|
/// | [APITokenSelector] | | select an existing API token from a drop-down list, and returns the GUID of the selected API token |
/// | [BooleanInput] | | choose either "Yes" or "No", and returns the value that was selected |
/// | [ConnectionCreator] | | create a new connection by providing a name and list of admins, and returns a string representation of the connection object that should be created |
/// | [ConnectionSelector] | | select an existing connection from a drop-down list, and returns the qualified name of the selected connection |
/// | [ConnectorTypeSelector] | | select from the types of connectors that exist in the tenant (for example "Snowflake"), without needing to select a specific instance of a connection (for example, the "production" connection for Snowflake); will return a string-encoded object giving the connection type that was selected and a list of all connections in the tenant that have that type |
/// | [CredentialInput] | | enter sensitive credential information that will be encrypted and protected in Atlan's Vault, and returns the GUID of the entry in the Vault |
/// | [DateInput] | | enter or select a date (not including time) from a calendar, and returns the epoch-based number representing that selected date in seconds |
/// | [DropDown] | | select from a drop-down of provided options |
/// | [FileCopier] | | copy a file from the tenant's S3 bucket, and returns the name of the file (as it is named in S3) |
/// | [FileUploader] | | upload a file, and returns the GUID-based name of the file (as it is renamed after upload) |
/// | [KeygenInput] | | generate a unique key that could be used for securing an exchange or other unique identification purposes, and provides buttons to regenerate the key or copy its text; will return the generated key as clear text |
/// | [MultipleGroups] | | choose multiple groups, and returns an array of group names that were selected |
/// | [MultipleUsers] | | choose multiple users, and returns an array of usernames that were selected |
/// | [NumericInput] | | enter an arbitrary number into a single-line text input field, and returns the value of the number that was entered |
/// | [PasswordInput] | | enter arbitrary text, but the text will be shown as dots when entered rather than being displayed in clear text; will return the entered text in clear text |
/// | [Radio] | | select just one option from a set of options, and returns the key of the selected option (typically, this is used to control mutually exclusive options in the UI) |
/// | [SingleGroup] | | select a single group, and returns the group name of the selected group |
/// | [SingleUser] | | select a single user, and returns the username of the selected user |
/// | [TextInput] | | enter arbitrary text into a single-line text input field, and returns the value of the text that was entered |
abstract class UIElement {
  /// Type of the element, which can be used to determine how to render it in the UI.
  fixed type: String

  /// Name to show in the UI for the widget.
  hidden title: String

  /// Whether a value must be selected to proceed with the UI setup.
  required: Boolean = false

  /// Whether the widget will be shown in the UI (false) or not (true).
  hidden hide: Boolean = false

  /// Informational text to place in a hover-over to describe the use of the input.
  hidden helpText: String = ""

  /// Sizing of the input on the UI (8 is full-width, 4 is half-width).
  hidden width: Int = 8

  /// Default fallback value to use for the element if nothing is selected or entered in the UI.
  hidden fallback: Any? = null

  /// (Generated) UI configuration for the element.
  fixed ui: Widget = new Widget {
    label = title
    `hidden` = hide
    help = helpText
    grid = width
  }
}

/// Base class for all UI elements that use some enumeration of valid values.
abstract class UIElementWithEnum extends UIElement {

  /// Mapping of possible values for the input, keyed by a unique (variable) name for the value.
  hidden possibleValues: Mapping<String, String>

  /// Default value to select in the input.
  default: String?

  /// (Generated) Listing of the unique variable names for the possible values.
  fixed enum: List<String> = possibleValues.fold(List(), (acc: List<String>, key, _) -> acc.add(key))

  /// (Generated) Listing of the display values for each possible value.
  fixed enumNames: List<String> = possibleValues.fold(List(), (acc: List<String>, _, value) -> acc.add(value))
}

/// Base class for all generated elements of the UI.
class Widget {

  /// Internal name of this widget, that controls what kind of widget is rendered in the UI.
  widget: String

  /// Label to show in the UI for the widget.
  label: String

  /// Whether the widget will be shown in the UI (false) or not (true).
  `hidden`: Boolean = false

  /// Informational text to place in a hover-over to describe the use of the input.
  help: String = ""

  /// Example text to place within the widget to exemplify its use.
  placeholder: String?

  /// Sizing of the input on the UI (8 is full-width, 4 is half-width).
  grid: Int = 8

  /// Mode of the widget, if applicable (usually whether something allows multiple selections or only a single selection).
  mode: String?

  /// TBC
  start: Int?

  /// List of the mime-types of files that should be accepted.
  accept: List<String>?

  /// Whether the widget should return file metadata.
  fileMetadata: Boolean?

  /// Most distant past value that can be selected through the widget.
  min: Int?

  /// Most distant future value that can be selected through the widget.
  max: Int?

  /// Default value to select in the widget.
  default: Any?

  /// Type of credential to be nested within the widget.
  credentialType: String?

  /// TBC
  nestedValue: Boolean?

  /// Fixed string that should appear (immutable) before the input box.
  addonBefore: String?

  /// Whether the value can be changed (false) or is immutable (true).
  disabled: Boolean?

  /// SQL query to run to determine the widget's selectable content.
  query: String?

  /// Whether to prevent the Test Authentication button from showing (true) or show it (false) for credential inputs.
  isTestAuthenticationDisabled: Boolean?
}

/// Widget that allows you to enter arbitrary text into a single-line text input field,
/// and returns the value of the text that was entered.
///
/// | Field | | Description | Default |
/// |---|---|---|---|
/// | **`title`** | | name to show in the UI for the widget | |
/// | `required` | | whether a value must be selected to proceed with the UI setup | `false` |
/// | `hide` | | whether the widget will be shown in the UI (false) or not (true) | `false` |
/// | `helpText` | | informational text to place in a hover-over to describe the use of the input | `""` |
/// | `placeholderText` | | example text to place within the widget to exemplify its use | `""` |
/// | `width` | | sizing of the input on the UI (8 is full-width, 4 is half-width) | `8` |
/// | `defaultValue` | | default value to use in the widget | |
/// | `prepend` | | fixed text to prepend to the value entered by the user | |
/// | `enabled` | | whether the value can be changed (true) or is immutable (false) | |
class TextInput extends UIElement {
  fixed type = "string"
  required: Boolean = false // Necessary for generated Kotlin copy method override

  /// Example text to place within the widget to exemplify its use.
  hidden placeholderText: String = ""

  /// (Optional) Default value to use in the widget.
  hidden defaultValue: String?

  /// (Optional) Fixed text to prepend to the value entered by the user.
  hidden prepend: String?

  /// (Optional) Whether the value can be changed (true) or is immutable (false).
  hidden enabled: Boolean?

  /// (Generated) Internal configuration for the UI's rendering.
  fixed ui {
    widget = "input"
    placeholder = placeholderText
    addonBefore = prepend
    disabled = if (enabled != null) !enabled else null
    default = defaultValue
  }
}

/// Widget that allows you to enter arbitrary text into a multi-line text input box,
/// and returns the value of the text that was entered.
///
/// | Field | | Description | Default |
/// |---|---|---|---|
/// | **`title`** | | name to show in the UI for the widget | |
/// | `required` | | whether a value must be selected to proceed with the UI setup | `false` |
/// | `hide` | | whether the widget will be shown in the UI (false) or not (true) | `false` |
/// | `helpText` | | informational text to place in a hover-over to describe the use of the input | `""` |
/// | `placeholderText` | | example text to place within the widget to exemplify its use | `""` |
/// | `width` | | sizing of the input on the UI (8 is full-width, 4 is half-width) | `8` |
class TextBoxInput extends UIElement {
  fixed type = "string"
  required: Boolean = false // Necessary for generated Kotlin copy method override

  /// Example text to place within the widget to exemplify its use.
  hidden placeholderText: String = ""

  /// (Generated) Internal configuration for the UI's rendering.
  fixed ui {
    widget = "TextInput"
    placeholder = placeholderText
  }
}

/// Widget that allows you to select just one option from a set of options, and returns the key of
/// the selected option.
/// Typically, this is used to control mutually exclusive options in the UI.
///
/// | Field | | Description | Default |
/// |---|---|---|---|
/// | **`title`** | | name to show in the UI for the widget | |
/// | **`possibleValues`** | | possible values that can be selected in the radio button | |
/// | **`default`** | | default value to select in the radio button (its key) | |
/// | `required` | | whether a value must be selected to proceed with the UI setup | `false` |
/// | `hide` | | whether the widget will be shown in the UI (false) or not (true) | `false` |
/// | `helpText` | | informational text to place in a hover-over to describe the use of the input | `""` |
class Radio extends UIElementWithEnum {
  fixed type = "string"

  /// Possible values that can be selected in the radio button.
  /// The key is the value that will be returned, and the value is the text to display.
  hidden possibleValues: Mapping<String, String>

  /// Default value to select in the radio button (its key).
  default: String?(if (this != null) possibleValues.keys.contains(this) else true)

  /// (Generated) Internal configuration for the UI's rendering.
  fixed ui {
    widget = "radio"
  }
}

/// Widget that allows you to create a new connection by providing a name and list of admins,
/// and returns a string representation of the connection object that should be created.
///
/// | Field | | Description | Default |
/// |---|---|---|---|
/// | **`title`** | | name to show in the UI for the widget | |
/// | `required` | | whether a value must be selected to proceed with the UI setup | `false` |
/// | `hide` | | whether the widget will be shown in the UI (false) or not (true) | `false` |
/// | `helpText` | | informational text to place in a hover-over to describe the use of the input | `""` |
class ConnectionCreator extends UIElement {
  fixed type = "string"
  required: Boolean = false // Necessary for generated Kotlin copy method override

  /// (Generated) Internal configuration for the UI's rendering.
  fixed ui {
    widget = "connection"
  }
}

/// Widget that allows you to select an existing connection from a drop-down list,
/// and returns the qualified name of the selected connection.
///
/// | Field | | Description | Default |
/// |---|---|---|---|
/// | **`title`** | | name to show in the UI for the widget | |
/// | `required` | | whether a value must be selected to proceed with the UI setup | `false` |
/// | `hide` | | whether the widget will be shown in the UI (false) or not (true) | `false` |
/// | `helpText` | | informational text to place in a hover-over to describe the use of the input | `""` |
/// | `multiSelect` | | whether multiple connections can be selected (true) or only a single connection (false) | `false` |
/// | `begin` | | TBC | `1` |
class ConnectionSelector extends UIElement {
  fixed type = "string"
  required: Boolean = false // Necessary for generated Kotlin copy method override

  /// Whether multiple connections can be selected (true) or only a single connection (false).
  hidden multiSelect: Boolean = false

  /// TBC
  hidden begin: Int = 1

  /// (Generated) Internal configuration for the UI's rendering.
  fixed ui {
    widget = "connectionSelector"
    mode = if (multiSelect) "multiple" else ""
    start = begin
  }
}

/// Widget that allows you to upload a file, and returns the GUID-based name of the file (as it is
/// renamed after upload).
///
/// | Field | | Description | Default |
/// |---|---|---|---|
/// | **`title`** | | name to show in the UI for the widget | |
/// | **`fileTypes`** | | list of the mime-types of files that should be accepted | |
/// | `required` | | whether a value must be selected to proceed with the UI setup | `false` |
/// | `hide` | | whether the widget will be shown in the UI (false) or not (true) | `false` |
/// | `helpText` | | informational text to place in a hover-over to describe the use of the input | `""` |
/// | `placeholderText` | | example text to place within the widget to exemplify its use | `""` |
class FileUploader extends UIElement {
  fixed type = "string"
  required: Boolean = false // Necessary for generated Kotlin copy method override

  /// List of the mime-types of files that should be accepted.
  hidden fileTypes: Listing<String>

  /// Example text to place within the widget to exemplify its use.
  hidden placeholderText: String = ""

  /// (Generated) Internal configuration for the UI's rendering.
  fixed ui {
    widget = "fileUpload"
    placeholder = placeholderText
    accept = fileTypes.toList()
    fileMetadata = true
  }
}

/// Widget that allows you to copy a file from the tenant's S3 bucket, and returns the name of the file
/// (as it is named in S3).
///
/// | Field | | Description | Default |
/// |---|---|---|---|
/// | **`title`** | | name to show in the UI for the widget | |
/// | `required` | | whether a value must be entered to proceed with the UI setup | `false` |
/// | `hide` | | whether the widget will be shown in the UI (false) or not (true) | `false` |
/// | `helpText` | | informational text to place in a hover-over to describe the use of the input | `""` |
/// | `placeholderText` | | example text to place within the widget to exemplify its use | `""` |
/// | `width` | | sizing of the input on the UI (8 is full-width, 4 is half-width) | `8` |
class FileCopier extends UIElement {
  fixed type = "string"
  required: Boolean = false // Necessary for generated Kotlin copy method override

  /// Example text to place within the widget to exemplify its use.
  hidden placeholderText: String = ""

  /// (Generated) Internal configuration for the UI's rendering.
  fixed ui {
    widget = "input"
    placeholder = placeholderText
  }
}

/// Widget that allows you to enter an arbitrary number into a single-line text input field,
/// and returns the value of the number that was entered.
///
/// | Field | | Description | Default |
/// |---|---|---|---|
/// | **`title`** | | name to show in the UI for the widget | |
/// | `required` | | whether a value must be selected to proceed with the UI setup | `false` |
/// | `hide` | | whether the widget will be shown in the UI (false) or not (true) | `false` |
/// | `helpText` | | informational text to place in a hover-over to describe the use of the input | `""` |
/// | `placeholderValue` | | example value to place within the widget to exemplify its use | |
/// | `width` | | sizing of the input on the UI (8 is full-width, 4 is half-width) | `8` |
/// | `enabled` | | whether the value can be changed (true) or is immutable (false) | |
class NumericInput extends UIElement {
  fixed type = "number"
  required: Boolean = false // Necessary for generated Kotlin copy method override

  /// Example text to place within the widget to exemplify its use.
  hidden placeholderValue: Number?

  /// Default number to use in the widget.
  default: Number?

  /// (Optional) Whether the value can be changed (true) or is immutable (false).
  hidden enabled: Boolean?

  /// (Generated) Internal configuration for the UI's rendering.
  fixed ui {
    widget = "inputNumber"
    placeholder = placeholderValue?.toString()
    disabled = if (enabled != null) !enabled else null
  }
}

/// Widget that allows you to enter or select a date (not including time) from a calendar,
/// and returns the epoch-based number representing that selected date in seconds.
///
/// | Field | | Description | Default |
/// |---|---|---|---|
/// | **`title`** | | name to show in the UI for the widget | |
/// | `required` | | whether a value must be selected to proceed with the UI setup | `false` |
/// | `hide` | | whether the widget will be shown in the UI (false) or not (true) | `false` |
/// | `helpText` | | informational text to place in a hover-over to describe the use of the input | `""` |
/// | `past` | | an offset from today (0) that indicates how far back in the calendar can be selected (-1 is yesterday, 1 is tomorrow, and so on) | `-14` |
/// | `future` | | an offset from today (0) that indicates how far forward in the calendar can be selected (-1 is yesterday, 1 is tomorrow, and so on) | `0` |
/// | `defaultDay` | | an offset from today that indicates the default date that should be selected in the calendar (0 is today, -1 is yesterday, 1 is tomorrow, and so on) | `0` |
/// | `begin` | | TBC | `1` |
/// | `width` | | sizing of the input on the UI (8 is full-width, 4 is half-width) | `8` |
class DateInput extends UIElement {
  fixed type = "number"
  required: Boolean = false // Necessary for generated Kotlin copy method override

  /// An offset from today (0) that indicates how far back in the calendar can be selected (-1 is yesterday, 1 is tomorrow, and so on).
  hidden past: Int = -14

  /// An offset from today (0) that indicates how far forward in the calendar can be selected (-1 is yesterday, 1 is tomorrow, and so on).
  hidden future: Int = 0

  /// An offset from today that indicates the default date that should be selected in the calendar (0 is today, -1 is yesterday, 1 is tomorrow, and so on).
  hidden defaultDay: Int = 0

  /// TBC
  hidden begin: Int = 1

  /// (Generated) Internal configuration for the UI's rendering.
  fixed ui {
    widget = "date"
    start = begin
    min = past
    max = future
    default = defaultDay
  }
}

/// Widget that allows you to choose either "Yes" or "No",
/// and returns the value that was selected.
///
/// | Field | | Description | Default |
/// |---|---|---|---|
/// | **`title`** | | name to show in the UI for the widget | |
/// | `required` | | whether a value must be selected to proceed with the UI setup | `false` |
/// | `hide` | | whether the widget will be shown in the UI (false) or not (true) | `false` |
/// | `defaultSelection` | | the default value to use for this boolean input | `false` |
/// | `helpText` | | informational text to place in a hover-over to describe the use of the input | `""` |
class BooleanInput extends UIElement {
  fixed type = "boolean"
  required: Boolean = false // Necessary for generated Kotlin copy method override

  /// The default value to use for this boolean input.
  hidden defaultSelection: Boolean = false

  /// (Generated) Internal configuration for the UI's rendering.
  fixed ui {
    widget = "boolean"
    default = defaultSelection
  }
}

/// Widget that allows you to select from a drop-down of provided options.
///
/// | Field | | Description | Default |
/// |---|---|---|---|
/// | **`title`** | | name to show in the UI for the widget | |
/// | **`possibleValues`** | | map of option keys to the value that will be display for each option in the drop-down | |
/// | `required` | | whether a value must be selected to proceed with the UI setup | `false` |
/// | `hide` | | whether the widget will be shown in the UI (false) or not (true) | `false` |
/// | `helpText` | | informational text to place in a hover-over to describe the use of the input | `""` |
/// | `multiSelect` | | whether multiple options can be selected (true) or only a single option (false) | `false` |
/// | `width` | | sizing of the input on the UI (8 is full-width, 4 is half-width) | `8` |
class DropDown extends UIElementWithEnum {
  fixed type = "string"
  required: Boolean = false // Necessary for generated Kotlin copy method override

  /// Whether multiple values can be selected (true) or only a single value (false).
  hidden multiSelect: Boolean = false

  /// (Generated) Internal configuration for the UI's rendering.
  fixed ui {
    widget = "select"
    mode = if (multiSelect) "multiple" else ""
  }
}

/// Widget that allows you to choose multiple groups,
/// and returns an array of group names that were selected.
///
/// | Field | | Description | Default |
/// |---|---|---|---|
/// | **`title`** | | name to show in the UI for the widget | |
/// | `required` | | whether a value must be selected to proceed with the UI setup | `false` |
/// | `hide` | | whether the widget will be shown in the UI (false) or not (true) | `false` |
/// | `helpText` | | informational text to place in a hover-over to describe the use of the input | `""` |
/// | `width` | | sizing of the input on the UI (8 is full-width, 4 is half-width) | `8` |
class MultipleGroups extends UIElement {
  fixed type = "string"
  required: Boolean = false // Necessary for generated Kotlin copy method override

  /// (Generated) Internal configuration for the UI's rendering.
  fixed ui {
    widget = "groupMultiple"
  }
}

/// Widget that allows you to select a single group,
/// and returns the group name of the selected group.
///
/// | Field | | Description | Default |
/// |---|---|---|---|
/// | **`title`** | | name to show in the UI for the widget | |
/// | `required` | | whether a value must be selected to proceed with the UI setup | `false` |
/// | `hide` | | whether the widget will be shown in the UI (false) or not (true) | `false` |
/// | `helpText` | | informational text to place in a hover-over to describe the use of the input | `""` |
/// | `width` | | sizing of the input on the UI (8 is full-width, 4 is half-width) | `8` |
class SingleGroup extends UIElement {
  fixed type = "string"
  required: Boolean = false // Necessary for generated Kotlin copy method override

  /// (Generated) Internal configuration for the UI's rendering.
  fixed ui {
    widget = "groups"
  }
}

/// Widget that allows you to choose multiple users,
/// and returns an array of usernames that were selected.
///
/// | Field | | Description | Default |
/// |---|---|---|---|
/// | **`title`** | | name to show in the UI for the widget | |
/// | `required` | | whether a value must be selected to proceed with the UI setup | `false` |
/// | `hide` | | whether the widget will be shown in the UI (false) or not (true) | `false` |
/// | `helpText` | | informational text to place in a hover-over to describe the use of the input | `""` |
/// | `width` | | sizing of the input on the UI (8 is full-width, 4 is half-width) | `8` |
class MultipleUsers extends UIElement {
  fixed type = "string"
  required: Boolean = false // Necessary for generated Kotlin copy method override

  /// (Generated) Internal configuration for the UI's rendering.
  fixed ui {
    widget = "userMultiple"
  }
}

/// Widget that allows you to select a single user,
/// and returns the username of the selected user.
///
/// | Field | | Description | Default |
/// |---|---|---|---|
/// | **`title`** | | name to show in the UI for the widget | |
/// | `required` | | whether a value must be selected to proceed with the UI setup | `false` |
/// | `hide` | | whether the widget will be shown in the UI (false) or not (true) | `false` |
/// | `helpText` | | informational text to place in a hover-over to describe the use of the input | `""` |
/// | `width` | | sizing of the input on the UI (8 is full-width, 4 is half-width) | `8` |
class SingleUser extends UIElement {
  fixed type = "string"
  required: Boolean = false // Necessary for generated Kotlin copy method override

  /// (Generated) Internal configuration for the UI's rendering.
  fixed ui {
    widget = "users"
  }
}

/// Widget that allows you to select from the types of connectors that exist in the tenant
/// (for example "Snowflake"), without needing to select a specific instance of a connection
/// (for example, the "production" connection for Snowflake). Will return a string-encoded
/// object giving the connection type that was selected and a list of all connections in the tenant
/// that have that type.
///
/// | Field | | Description | Default |
/// |---|---|---|---|
/// | **`title`** | | name to show in the UI for the widget | |
/// | `required` | | whether a value must be selected to proceed with the UI setup | `false` |
/// | `hide` | | whether the widget will be shown in the UI (false) or not (true) | `false` |
/// | `helpText` | | informational text to place in a hover-over to describe the use of the input | `""` |
/// | `width` | | sizing of the input on the UI (8 is full-width, 4 is half-width) | `8` |
/// | `begin` | | TBC | `1` |
class ConnectorTypeSelector extends UIElement {
  fixed type = "string"
  required: Boolean = false // Necessary for generated Kotlin copy method override

  /// TBC
  hidden begin: Int = 1

  /// (Generated) Internal configuration for the UI's rendering.
  fixed ui {
    widget = "sourceConnectionSelector"
    start = begin
  }
}

/// Widget that allows you to select an existing API token from a drop-down list,
/// and returns the GUID of the selected API token.
/// Note: currently only API tokens that were created by the user configuring the workflow
/// will appear in the drop-down list.
///
/// | Field | | Description | Default |
/// |---|---|---|---|
/// | **`title`** | | name to show in the UI for the widget | |
/// | `required` | | whether a value must be selected to proceed with the UI setup | `false` |
/// | `hide` | | whether the widget will be shown in the UI (false) or not (true) | `false` |
/// | `helpText` | | informational text to place in a hover-over to describe the use of the input | `""` |
class APITokenSelector extends UIElement {
  fixed type = "string"
  required: Boolean = false // Necessary for generated Kotlin copy method override

  /// (Generated) Internal configuration for the UI's rendering.
  fixed ui {
    widget = "apiTokenSelect"
  }
}

/// Widget that allows you to generate a unique key that could be used for securing an exchange
/// or other unique identification purposes, and provides buttons to regenerate the key or copy its
/// text. Will return the generated key as clear text.
///
/// | Field | | Description | Default |
/// |---|---|---|---|
/// | **`title`** | | name to show in the UI for the widget | |
/// | `required` | | whether a value must be selected to proceed with the UI setup | `false` |
/// | `hide` | | whether the widget will be shown in the UI (false) or not (true) | `false` |
/// | `helpText` | | informational text to place in a hover-over to describe the use of the input | `""` |
/// | `width` | | sizing of the input on the UI (8 is full-width, 4 is half-width) | `8` |
class KeygenInput extends UIElement {
  fixed type = "string"
  required: Boolean = false // Necessary for generated Kotlin copy method override

  /// (Generated) Internal configuration for the UI's rendering.
  fixed ui {
    widget = "keygen"
  }
}

/// Widget that allows you to enter arbitrary text, but the text will be shown as dots when entered
/// rather than being displayed in clear text. Will return the entered text in clear text.
///
/// | Field | | Description | Default |
/// |---|---|---|---|
/// | **`title`** | | name to show in the UI for the widget | |
/// | `required` | | whether a value must be selected to proceed with the UI setup | `false` |
/// | `hide` | | whether the widget will be shown in the UI (false) or not (true) | `false` |
/// | `helpText` | | informational text to place in a hover-over to describe the use of the input | `""` |
/// | `width` | | sizing of the input on the UI (8 is full-width, 4 is half-width) | `8` |
class PasswordInput extends UIElement {
  fixed type = "string"
  required: Boolean = false // Necessary for generated Kotlin copy method override

  /// (Generated) Internal configuration for the UI's rendering.
  fixed ui {
    widget = "password"
  }
}

/// Widget that allows you to enter sensitive credential information that will be encrypted and protected
/// in Atlan's Vault and not be visible to anyone. Will return the GUID of the entry in the Vault.
///
/// | Field | | Description | Default |
/// |---|---|---|---|
/// | **`title`** | | name to show in the UI for the widget | |
/// | **`credType`** | | type of credential to be nested within the widget | |
/// | `required` | | whether a value must be selected to proceed with the UI setup | `false` |
/// | `hide` | | whether the widget will be shown in the UI (false) or not (true) | `false` |
/// | `helpText` | | informational text to place in a hover-over to describe the use of the input | `""` |
class CredentialInput extends UIElement {
  fixed type = "string"
  required: Boolean = false // Necessary for generated Kotlin copy method override

  /// Type of credential to be nested within the widget.
  hidden credType: String

  /// Whether to show the button for testing authentication. (Default: true, show it)
  hidden allowTestAuthentication: Boolean = true

  /// (Generated) Internal configuration for the UI's rendering.
  fixed ui {
    widget = "credential"
    credentialType = credType
    isTestAuthenticationDisabled = !allowTestAuthentication
  }
}

/// Widget that allows you to configure multiple sub-elements all grouped together under a single parent
/// variable.
///
/// | Field | | Description | Default |
/// |---|---|---|---|
/// | **`title`** | | name to show in the UI for the widget | |
/// | **`inputs`** | | map of the sub-elements that should be nested within this input | |
/// | `helpText` | | informational text to place in a hover-over to describe the use of the input | `""` |
/// | `width` | | sizing of the input on the UI (8 is full-width, 4 is half-width) | `8` |
open class NestedInput extends UIElement {
  fixed type = "object"

  /// Whether the widget will be shown in the UI (false) or not (true).
  hidden hide: Boolean = false // Necessary for generated Kotlin copy method override

  /// Map of the sub-elements that should be nested within this input, keyed by unique lower_snake_case variable name.
  hidden inputs: Mapping<String(lower_snake_case), UIElement>

  /// (Generated) Map of properties nested within this input.
  fixed properties: Map<String, UIElement> = inputs.toMap()

  /// (Generated) Internal configuration for the UI's rendering.
  fixed ui {
    widget = "nested"
    nestedValue = false
    `hidden` = hide
  }
}

/// Widget that executes arbitrary SQL against the connector that is being configured.
///
/// | Field | | Description | Default |
/// |---|---|---|---|
/// | **`title`** | | name to show in the UI for the widget | |
/// | **`sqlQuery`** | | query to run in the SQL executor | |
/// | `helpText` | | informational text to place in a hover-over to describe the use of the input | `""` |
/// | `width` | | sizing of the input on the UI (8 is full-width, 4 is half-width) | `8` |
class SQLExecutor extends UIElement {
  fixed type = "string"
  required: Boolean = false // Necessary for generated Kotlin copy method override

  /// Query to run in the SQL executor.
  hidden sqlQuery: String

  /// (Generated) Internal configuration for the UI's rendering.
  fixed ui {
    widget = "sql"
    query = sqlQuery
  }
}
